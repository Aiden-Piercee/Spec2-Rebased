













module SaveReplayRoot
{
    attribute popup;
    attribute closed;
    attribute mode;

    method open(context, mode)
    {
        self.mode = mode;
        self.closed = false;
        self.popup = main::SequenceUtil::ModalPage(context, SaveReplayRoot);
        self.popup.open();
        self.mode = nil;
        self.closed = nil;
        return true;
    }

    method close(context, ok)
    {
        if (self.closed)
            return;

        self.closed = true;

        if (ok)
        {
            self.popup.close();
            self.popup = nil;
        }
        else
        {
            FadeActor.on_effect_end = (context) => 
            {
                if (SaveReplayRoot.popup != nil)
                {
                    SaveReplayRoot.popup.close();
                    SaveReplayRoot.popup = nil;
                }
            };
            FadeActor.out = false;
        }
    }

    method onInitialize(context)
    {
        return EVENTRESULT_FILTER;
    }

    method onFinalize(context)
    {
        return EVENTRESULT_FILTER;
    }

    method onCancel(context)
    {
        sound.play("cancel");
        SaveReplayRoot.setFocus(DialogFrame::Pane::Body::Buttons::Cancel);
        return EVENTRESULT_FILTER;
    }

    function saveReplay(context)
    {
        var mode = SaveReplayRoot.mode;

        if (!ReplayFileUtil::canSave(mode))
        {
            DialogUtil::openConfirmDialog(context, DialogUtil::ERROR, 
                manager.translate("QuickMenu", "NO_DATA_TO_BE_SAVED"));
            return;
        }

        context.cursor_visible = false;

        var res = ReplayFileUtil::save(context, mode);
        SaveReplayRoot::FadeActor.out = false;

        if (res)
        {
            DialogUtil::openConfirmDialogAutoClose(context, DialogUtil::OK, 
                manager.translate("QuickMenu", "SAVE_DONE"), nil, 1.0);
        }
        else
        {
            DialogUtil::openConfirmDialog(context, DialogUtil::ERROR, 
                manager.translate("QuickMenu", "SAVE_FAILED"));
        }

        context.cursor_visible = true;
    }

    module DialogFrame::Pane
    {
        module Body
        {
            module Buttons
            {
                module OK
                {
                    method onActivate(context)
                    {
                        sound.play("ok");
                        saveReplay(context);
                        SaveReplayRoot.close(context, true);
                        return EVENTRESULT_FILTER;
                    }
                }

                module Cancel
                {
                    method onActivate(context)
                    {
                        sound.play("ok");
                        SaveReplayRoot.close(context, false);
                        return EVENTRESULT_FILTER;
                    }
                }
            }
        }
    }
}