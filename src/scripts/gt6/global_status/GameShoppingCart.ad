

//---------------------------------------------------------------------------------------
// GameShoppingCart.ad
//
// Creates the save profile entity
// 
//---------------------------------------------------------------------------------------


//---------------------------------------------------------------------------------------
// module
//---------------------------------------------------------------------------------------
module GameShoppingCart
{
    static ERROR_SUCCESS = 0;
    static ERROR_CART_FULL = 1;
    static ERROR_NO_SKU = 2;
    static ERROR_DUPLICATE = 3;

    function clearInitializer()
    {
        undef CreateStructure;
        undef Initialize;
        undef clearInitializer;
    }

    function CreateStructure()
    {
        var st = STStructure("Impl");
        GameShoppingCart.defineModule(st);
        
        var st_bag = STStructure();
        st_bag.define("tv_id", STInt());
        st_bag.define("type", STString(8));
        st_bag.define("sku", STString(64));
        st.define("bag", STArray(st_bag, 16));

        DefineMethod();

        return st;
    }


    function Initialize(instance)
    {
        for (var i = 0; i < 16; ++i)
        {
            var _bag = instance.bag[i];
            _bag.tv_id = 0;
            _bag.type = "";
            _bag.sku = "";
        }
    }

    function DefineMethod()
    {
        module Impl
        {
            method clear()
            {
                for (var i = 0; i < 16; ++i)
                {
                    _setBag(self.bag[i], 0, "", "");
                }
            }

            method add(tv_id, type, sku)
            {
                if (sku == nil || sku == "")
                    return ERROR_NO_SKU;

                if (isExist(tv_id, type, sku))
                    return ERROR_DUPLICATE;

                var result = ERROR_CART_FULL;
                foreach (var _bag in self.bag)
                {
                    if (_isEmptyBag(_bag))
                    {
                        _setBag(_bag, tv_id, type, sku);
                        result = ERROR_SUCCESS;
                        break;
                    }
                }

                return result;
            }

            method delete(tv_id, type)
            {
                var result = false;
                foreach (var _bag in self.bag)
                {
                    if (_isEqualsBag(_bag, tv_id, type))
                    {
                        _setBag(_bag, 0, "", "");
                        result = true;
                        break;
                    }
                }

                return result;
            }

            method getCount()
            {
                var count = 0;
                foreach (var _bag in self.bag)
                {
                    if (!_isEmptyBag(_bag))
                        ++count;
                }

                return count;
            }

            method isExist(tv_id, type, sku)
            {
                foreach (var _bag in self.bag)
                {
                    if (_bag.tv_id == tv_id && _bag.type == type || _bag.sku == sku)
                        return true;
                }

                return false;
            }

            method getList()
            {
                var list = [];
                foreach (var _bag in self.bag)
                {
                    if (!_isEmptyBag(_bag))
                    {
                        list.push([
                            "tv_id" : _bag.tv_id,
                            "type" : _bag.type,
                            "sku" : _bag.sku,
                        ]);
                    }
                }

                return list;
            }

            method _setBag(_bag, tv_id, type, sku)
            {
                _bag.tv_id = tv_id;
                _bag.type = type;
                _bag.sku = sku;
            }

            method _isEmptyBag(_bag)
            {
                return 0 == _bag.tv_id;
            }
            
            method _isEqualsBag(_bag, tv_id, type)
            {
                return _bag.tv_id == tv_id && _bag.type == type;
            }
        }
    }
}