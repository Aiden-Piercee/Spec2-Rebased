

//---------------------------------------------------------------------------------------
// GameEntitlement.ad
//
// Creates the save profile entity
// 
//---------------------------------------------------------------------------------------


//---------------------------------------------------------------------------------------
// module
//---------------------------------------------------------------------------------------
module GameEntitlement
{
    function clearInitializer()
    {
        undef CreateStructure;
        undef Initialize;
        undef clearInitializer;
    }

    function CreateStructure()
    {
        var st = STStructure("Impl");
        GameEntitlement.defineModule(st);
        
        var st_bag = STStructure();
        st_bag.define("entitlement_id", STString(32));
        st_bag.define("consumed_count", STInt());
        st.define("bag", STArray(st_bag, 16));

        DefineMethod();

        return st;
    }

    function Initialize(instance)
    {
        for (var i = 0; i < 16; ++i)
        {
            var _bag = instance.bag[i];
            _bag.entitlement_id = "";
            _bag.consumed_count = 0;
        }
    }

    function DefineMethod()
    {
        module Impl
        {
            method consume(entitlement_id)
            {
                var found = false;
                foreach (var _bag in self.bag)
                {
                    if (_bag.entitlement_id == entitlement_id)
                    {
                        _bag.consumed_count++;
                        found = true;
                        break;
                    }
                }

                if (!found)
                {
                    foreach (var _bag in self.bag)
                    {
                        if (_bag.entitlement_id == "")
                        {
                            _bag.entitlement_id = entitlement_id;
                            _bag.consumed_count = 1;
                            break;
                        }
                    }
                }
            }

            method getConsumedCount(entitlement_id)
            {
                foreach (var _bag in self.bag)
                {
                    if (_bag.entitlement_id == entitlement_id)
                    {
                        return _bag.consumed_count;
                    }
                }
                return 0;
            }

            method clear()
            {
                foreach (var _bag in self.bag)
                {
                    _bag.entitlement_id = "";
                    _bag.consumed_count = 0;
                }
            }
        }
    }
}