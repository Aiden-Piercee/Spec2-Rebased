

//---------------------------------------------------------------------------------------
// TireUtil.ad
//
// Utility module for safely getting tire names from index
// 
//---------------------------------------------------------------------------------------


//---------------------------------------------------------------------------------------
// module
//---------------------------------------------------------------------------------------
module TireUtil
{
    static CategoryTrial = 0;
    static CategoryDirt = 2;
    static CategorySnow = 3;
    static DirtTire = 9;
    static SnowTire = 10;

    function getIndexRangeFromCategory(category)
    {
        switch (category)
        {
            case CategoryTrial:
                return [0, 8];

            case CategoryDirt:
                return [9, 9];

            case CategorySnow:
                return [11, 11];
        }

        return nil;
    }

    function getCategoryFromIndex(tire_index)
    {
        var category_list = [
            CategoryTrial,
            CategoryDirt,
            CategorySnow
        ];

        if (tire_index >= 100)
            tire_index -= 100;

        for (var i = 0; i < category_list.size; i++)
        {
            var cat = category_list[i];
            var range = getIndexRangeFromCategory(cat);
            if (tire_index >= range[0] && tire_index <= range[1])
                return cat;
        }

        return nil;
    }

    function convTireIndexToMenuIndex(tire_index)
    {
        var category = getCategoryFromIndex(tire_index);
        var range = getIndexRangeFromCategory(category);

        if (tire_index >= range[0] && tire_index <= range[1])
            return tire_index - range[0];
        else
            return nil;
    }

    function convMenuIndexToTireIndex(category, menu_index)
    {
        var range = getIndexRangeFromCategory(category);
        if (menu_index <= range[1] - range[0])
            return range[0] + menu_index;

        return nil;
    }

    function getMaxGripTireIndex(category)
    {
        var range = getIndexRangeFromCategory(category);
        return range[1];
    }

    function getName(context, tire_index, root)
    {
        if (tire_index == nil)
            return "-";

        if (root == nil)
            root = context.getRootWindow();

        return context.translate(root, "TireName", "name_%03d".format(tire_index));
    }

    function getNameShort(context, tire_index, root)
    {
        if (tire_index == nil)
            return "-";

        if (root == nil)
            root = context.getRootWindow();

        return context.translate(root, "TireName", "name_short_%03d".format(tire_index));
    }
}