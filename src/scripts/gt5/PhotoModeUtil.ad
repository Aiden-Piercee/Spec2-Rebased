

//---------------------------------------------------------------------------------------
// PhotoModeUtil.ad
//
// Utility module for dealing with photos
// 
//---------------------------------------------------------------------------------------


//---------------------------------------------------------------------------------------
// module
//---------------------------------------------------------------------------------------

module PhotoFileUtil
{
    module GENRE
    {
        static PHOTO_RACE = 1;
        static PHOTO_TRAVEL = 2;
        static PHOTO_PROFILE = 3;
    }

    static TABLE_VERSION = 110; // 1.10

    class Data
    {
        attribute id;
        attribute genre;
        attribute filename;
        attribute filesize;
        attribute filename_t;
        attribute filesize_t;
        attribute width;
        attribute height;
        attribute orientation;
        attribute title;
        attribute subtitle;
        attribute description;
        attribute car_label;
        attribute datetime;
        attribute snapshot_filename;
        attribute favorite;
        attribute online
        attribute vertical;
        attribute is_3d;
        attribute car_position_left;
        attribute car_position_top;
        attribute car_position_right;
        attribute car_position_bottom;
        attribute online_id;
        attribute grim_photo_id;
        attribute local_userid;

        method setup(data)
        {
            datetime = pdiext::getBetterTime().getBySecond();
            filesize = data.filesize;
            filesize_t = data.filesize_t;
            width = data.width;
            height = data.height;
            orientation = data.orientation;
            id = data.id;
            genre = data.genre;
            title = data.title;
            subtitle = data.subtitle;
            description = data.description;
            car_label = data.car_label;
            snapshot_filename = data.snapshot_filename;
            favorite = data.favorite;
            online = data.online;
            vertical = data.vertical;
            is_3d = data.is_3d;
            car_position_left = data.car_position_left;
            car_position_top = data.car_position_top;
            car_position_right = data.car_position_right;
            car_position_bottom = data.car_position_bottom;
            online_id = data.online_id;
            grim_photo_id = "";
            local_userid = getUserId();
        }
    }

    function initialize()
    {
        if (!DBUtil::initializeTable(USERDB, "t_photo_files", TABLE_VERSION))   
            return true; // Already exists

        var sql = "CREATE TABLE IF NOT EXISTS " "t_photo_files" " ("
                    "id INTEGER, "
                    "genre INTEGER, "
                    "filename TEXT, "
                    "filesize INTEGER, "
                    "filename_t TEXT, "
                    "filesize_t INTEGER, "
                    "width INTEGER, "
                    "height INTEGER, "
                    "orientation INTEGER, "
                    "title TEXT, "
                    "subtitle TEXT, "
                    "description TEXT, "
                    "car_label TEXT, "
                    "datetime INTEGER, "
                    "snapshot_filename TEXT, "
                    "favorite INTEGER, "
                    "online INTEGER, "
                    "vertical INTEGER, "
                    "is_3d INTEGER, "
                    "car_position_left REAL, "
                    "car_position_top REAL, "
                    "car_position_right REAL, "
                    "car_position_bottom REAL, "
                    "online_id TEXT, "
                    // There was something here
                    "grim_photo_id TEXT, "
                    "local_userid INTEGER, "
                    "PRIMARY KEY (id))";

        var res = USERDB.execute0(sql);
        DB_ASSERT(res == SQLITE_OK, res);

        return true;
    }

    function getTitleList(favorite)
    {
        var where = "WHERE local_userid = ?";
        var params = [getUserId()];

        if (favorite)
        {
            where += " AND favorite = ?";
            params.push(1);
        }

        var sql = "SELECT DISTINCT genre, title FROM %s %s ORDER BY title".format("t_photo_files", where);
        var rows = USERDB.executeQuery(sql, params);

        var result = Array(rows.size);
        for (var i = 0; i < rows.size; i++)
            result[i] = rows[i];

        return result;
    }

    function get(id)
    {
        var list = getListImpl("WHERE id = %{id}", nil, nil);
        return list.size > 0 ? list[0] : nil;
    }

    function getList(genre, title, order, by)
    {
        var conditions = [];
        if (genre != nil)
            conditions.push("genre = %{genre}");

        if (title != nil)
            conditions.push("title = '%{title}'");

        var where = "";
        if (conditions.size > 0)
            where = "WHERE " + conditions.join(" AND ");
        
        return getListImpl(where, order, by);
    }

    function getFavoriteList(title, format, order, by)
    {
        return getListImpl("WHERE favorite = 1", order, by);
    }

    function setFavorite(id, favorite)
    {
        var sql = "UPDATE %s SET favorite = %d WHERE id = %d".format("t_photo_files", favorite ? 1 : 0, id);
        var res = USERDB.execute(sql);
        DB_ASSERT(res == SQLITE_DONE, res);
    }

    function getFavoriteCount()
    {
        return getCount("WHERE favorite = 1");
    }

    function getFavoriteMax()
    {
        return 50;
    }

    function getOnlineList(order, by)
    {
        return getListImpl("WHERE online = 1", order, by);
    }

    function setOnline(id, online)
    {
        var sql = "UPDATE %s SET online = %d WHERE id = %d".format("t_photo_files", online ? 1 : 0, id);
        var res = USERDB.execute(sql);
        DB_ASSERT(res == SQLITE_DONE, res);
    }

    function setForceOffline()
    {
        var sql = "UPDATE %s SET online = 0".format("t_photo_files");
        var res = SQLITE.execute(sql);
        DB_ASSERT(res == SQLITE_DONE, res);
    }

    function getOnlineCount()
    {
        return getCount("WHERE online = 1");
    }

    function getOnlineMax()
    {
        return Grim2Util::Photo::getOnlineMax();
    }

    function getProfileBannerList(order, by)
    {
        return getListImpl("WHERE width = height", order, by);
    }

    function updateComment(id, comment)
    {
        var sql = "UPDATE %s SET description = ? WHERE id = %d".format("t_photo_files", id);
        var res = USERDB.execute(sql, [comment]);
        DB_ASSERT(res == SQLITE_DONE, res);
    }

    function updateGrimPhotoId(id, grim_photo_id)
    {
        var sql = "UPDATE %s SET grim_photo_id = ? WHERE id = %d".format("t_photo_files", id);
        var res = USERDB.execute(sql, [grim_photo_id]);
        DB_ASSERT(res == SQLITE_DONE, res);
    }

    function _save(work_path, thumb_path, data, play_id)
    {
        var path_base = IO::Path::GetDirName(ORG.photo_org_path) + "/" + "pht#%s#%016lx#".format(data.online_id, play_id);
        if (data.is_3d)
            data.filename = DBUtil::getUniqFileName(USERDB, "t_photo_files", path_base, "mpo");
        else
            data.filename = DBUtil::getUniqFileName(USERDB, "t_photo_files", path_base, "jpg");

        data.filename_t = IO::Path::StripExtension(data.filename) + "_t.jpg";
        data.local_userid = play_id;
        DBUtil::insert(USERDB, "t_photo_files", data);
        _removeFiles(data);

        pdistd::RenameFile(work_path, data.filename);
        pdistd::RenameFile(thumb_path, data.filename_t);
    }

    function save(context, work_path, thumb_path, photo_data)
    {
        var data = Data();
        data.setup(photo_data);

        var path_base = IO::Path::GetDirName(ORG.photo_org_path) + "/photo";
        if (data.is_3d)
            data.filename = DBUtil::getUniqFileName(USERDB, "t_photo_files", path_base, "mpo");
        else
            data.filename = DBUtil::getUniqFileName(USERDB, "t_photo_files", path_base, "jpg");

        data.filename_t = data.filename.removeLastOf(".jpg") + "_t.jpg";
        DBUtil::insert(USERDB, "t_photo_files", data);
        _removeFiles(data);

        pdistd::RenameFile(work_path, data.filename);
        pdistd::RenameFile(thumb_path, data.filename_t);
    }

    function _removeFiles(data)
    {
        if (pdistd::Exist(data.filename))
            pdistd::RemoveFile(data.filename);

        if (pdistd::Exist(data.filename_t))
            pdistd::RemoveFile(data.filename_t);
    }

    function remove(data)
    {
        _removeFiles(data);
        DBUtil::deleteRow(USERDB, "t_photo_files", data);

        if (GLOBAL_STATUS.user_profile.context.avatar_photo_id == data.id)
            GLOBAL_STATUS.user_profile.context.avatar_photo_id = 0;
    }

    function removeByList(data_list)
    {
        foreach (var data in data_list)
        {
            remove(data);
        }
    }

    function removeAll()
    {
        var list = getList();
        foreach (var data in list)
            _removeFiles(data);

        DBUtil::deleteAll(USERDB, "t_photo_files");
    }

    function isExist(id)
    {
        var sql = "SELECT id FROM %s WHERE id = ?".format("t_photo_files");
        var rows = USERDB.executeQuery(sql, [id]);
        return rows.size > 0;
    }

    function getLastProfilePhotoId()
    {
        var sql = "SELECT id FROM %s WHERE genre = ? ORDER BY id DESC LIMIT 1".format("t_photo_files");
        var rows = USERDB.executeQuery(sql, [GENRE::PHOTO_PROFILE]);
        var result = getResult1(rows);
        return result != nil ? result : -1;
    }

    function getTitleText(context, genre, title)
    {
        if (genre == GENRE::PHOTO_RACE)
            return manager.translate("CourseName", title);
        else if (genre == GENRE::PHOTO_TRAVEL)
            return manager.translate("PhotoMode", title);
        else
            return manager.translate("PhotoMode", "PROFILE_PHOTO_TITLE");
    }

    function getUserId()
    {
        return pdiapp::play_id;
    }

    function getListImpl(condition, order, by)
    {
        if (condition != "")
            condition += " AND local_userid = ?";
        else
            condition = "WHERE local_userid = ?";

        var sql = "%s %s %s".format(DBUtil::getSelectQuery("t_photo_files", Data),
                                    condition,
                                    createOrderBy(order, by));

        var res = USERDB.executeQuery(sql, [getUserId()]);
        return DBUtil::getDataList(Data, res);
    }

    function getCount(where)
    {
        var sql = "SELECT COUNT (*) FROM %s %s AND local_userid = ?".format("t_photo_files", where);
        var res = USERDB.executeQuery(sql, [getUserId()]);
        var count = getResult1(res);
        return count == nil ? 0 : count;
    }

    function createOrderBy(order, by)
    {
        if (nil == order)
            order = "datetime";

        if (nil == by)
            by = "ASC";

        var by2 = by;
        if ("datetime" == order || "filesize" == order)
        {
            if ("ASC" == by)
                by2 = "DESC";
            else if ("DESC" == by)
                by2 = "ASC";
        }

        return "order by %{order} %{by2}, id";
    }

    function getResult1(res)
    {
        if (res.size == 0)
            return nil;

        var row = res[0];
        if (row.size == 0)
            return nil;
        return res[0][0];
    }
}