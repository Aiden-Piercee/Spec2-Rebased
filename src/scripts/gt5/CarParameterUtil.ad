













module CarParameterUtil
{
    static SORT_BY_POWER = 1;
    static SORT_BY_PP = 2;
    static SORT_BY_NAME = 3;
    static SORT_BY_TUNER = 4;
    static SORT_BY_RGB = 5;
    static SORT_BY_OBTAIN = 6;

    function cmpByName(a, b)
    {
        var carname_a = main::gtengine::MSpecDB::getCarShortName(a.getCarCode());
        var carname_b = main::gtengine::MSpecDB::getCarShortName(b.getCarCode());
        if (carname_a < carname_b) // implicit strcmp
            return true;

        if (carname_a > carname_b)
            return false;

        // Fallback to more recent
        return a.obtain_id < b.obtain_id;
    }

    static dealer_sort_list = [
        "tuner_acura",
        "tuner_alfaromeo",
        "tuner_amemiya",
        "tuner_amuse",
        "tuner_artmorrison",
        "tuner_astonmartin",
        "tuner_audi",
        "tuner_blitz",
        "tuner_bmw",
        "tuner_chevrolet",
        "tuner_citroen",
        "tuner_polyphony",
        "tuner_daihatsu",
        "tuner_dodge",
        "tuner_ferrari",
        "tuner_ford",
        "tuner_honda",
        "tuner_jaguar",
        "tuner_lancia",
        "tuner_lexus",
        "tuner_lotus",
        "tuner_mazda",
        "tuner_mercedes",
        "tuner_mines",
        "tuner_mini",
        "tuner_mitsubishi",
        "tuner_nissan",
        "tuner_peugeot",
        "tuner_renault",
        "tuner_subaru",
        "tuner_suzuki",
        "tuner_tvr",
        "tuner_volkswagen",
    ];

    function cmpByTuner(a, b)
    {
        var tuner_code_a = main::gtengine::MSpecDB::getTunerCode(a.getCarCode());
        var tuner_code_b = main::gtengine::MSpecDB::getTunerCode(b.getCarCode());
        var sort_index_a = dealer_sort_list.indexOf(tuner_code_a);
        var sort_index_b = dealer_sort_list.indexOf(tuner_code_b);

        if (sort_index_a == sort_index_b)
            return cmpByName(a, b);

        // Fallback to more recent
        return sort_index_a < sort_index_b;
    }

    function cmpByPower(a, b)
    {
        var ps_a = a.getPowerScratch();
        var ps_b = b.getPowerScratch();
        if (ps_a == ps_b)
            return cmpByName(a, b);

        return ps_a > ps_b;
    }

    function sortList(sort_type, car_list, up = true)
    {
        switch (sort_type)
        {
            case SORT_BY_NAME:
                car_list.sort(cmpByName);
                break;

            case SORT_BY_TUNER:
                car_list.sort(cmpByTuner);
                break;

            case SORT_BY_POWER:
                for (var i = 0; i < car_list.size; i++)
                    car_list[i].updateScratch();

                car_list.sort(cmpByPower);
                break;
        }

        if (!up)
        {
            for (var i = 0, j = car_list.size - 1; i < j; )
            {
                var tmp = car_list[i];
                car_list[i] = car_list[j];
                car_list[j] = tmp;
                i++;
                j--;
            }
        }
    }
}